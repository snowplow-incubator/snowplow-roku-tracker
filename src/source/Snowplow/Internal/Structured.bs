' Copyright (c) 2021 Snowplow Analytics Ltd. All rights reserved.
'
' This program is licensed to you under the Apache License Version 2.0,
' and you may not use this file except in compliance with the Apache License Version 2.0.
' You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
'
' Unless required by applicable law or agreed to in writing,
' software distributed under the Apache License Version 2.0 is distributed on an
' "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
' See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 
import "pkg:/source/Snowplow/Internal/Event.bs"

namespace Snowplow.Internal
    class Structured extends Event
        category as string
        action as string
        label as string
        property as string
        value as double

        ' parameters
        private SE_CATEGORY = "se_ca"
        private SE_ACTION = "se_ac"
        private SE_LABEL = "se_la"
        private SE_PROPERTY = "se_pr"
        private SE_VALUE = "se_va"

        ' constants
        private SE_EVENT_STRUCTURED = "se"

        sub new(data as object)
            super(data)

            m.category = data[m.SE_CATEGORY]
            m.action = data[m.SE_ACTION]

            if data[m.SE_LABEL] <> invalid then m.label = data[m.SE_LABEL]
            if data[m.SE_PROPERTY] <> invalid then m.property = data[m.SE_PROPERTY]
            if data[m.SE_VALUE] <> invalid then m.value = data[m.SE_VALUE]
        end sub

        override function preparePayloadToSend() as object
            payload = super.preparePayloadToSend()

            payload[m.SE_CATEGORY] = m.category
            payload[m.SE_ACTION] = m.action

            if m.label <> invalid then payload[m.SE_LABEL] = m.label
            if m.property <> invalid then payload[m.SE_PROPERTY] = m.property
            if m.value <> invalid then payload[m.SE_VALUE] = m.value.ToStr()

            return payload
        end function

        override function getEventName() as dynamic
            return m.SE_EVENT_STRUCTURED
        end function

        override sub describe(logger)
            logger.debug(`+++ Structured Event: cat: ${m.category}, act: ${m.action}, lbl: ${m.label}, pr: ${m.property}, val: ${m.val}`)
            m.describeContext(logger)
        end sub
    end class
end namespace
