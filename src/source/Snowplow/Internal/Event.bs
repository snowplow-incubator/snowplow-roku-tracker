' Copyright (c) 2021 Snowplow Analytics Ltd. All rights reserved.
'
' This program is licensed to you under the Apache License Version 2.0,
' and you may not use this file except in compliance with the Apache License Version 2.0.
' You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
'
' Unless required by applicable law or agreed to in writing,
' software distributed under the Apache License Version 2.0 is distributed on an
' "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
' See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.

import "pkg:/source/Snowplow/Internal/EventContext.bs"
import "pkg:/source/Snowplow/Internal/Helpers.bs"

namespace Snowplow.Internal
    class Event
        trackerNamespace as string
        domainUserId as string
        domainSessionId as string
        domainSessionIndex as integer
        networkUserId as string
        userId as string
        appId as string
        eventId as string
        trackerVersion as string
        deviceTimestamp as string
        trueTimestamp as dynamic
        platform as string
        timezone as string
        language as string
        resolution as string
        viewport as string
        context as object

        ' parameters
        private E_SCHEMA = "schema"
        private E_DATA = "data"
        private E_EVENT = "e"
        private E_EID = "eid"
        private E_DEVICE_TIMESTAMP = "dtm"
        private E_TRUE_TIMESTAMP = "ttm"
        private E_SENT_TIMESTAMP = "stm"
        private E_TRACKER_VERSION = "tv"
        private E_APPID = "aid"
        private E_NAMESPACE = "tna"
        private E_PLATFORM = "p"
        private E_RESOLUTION = "res"
        private E_VIEWPORT = "vp"
        private E_TIMEZONE = "tz"
        private E_LANGUAGE = "lang"
        private E_NETWORK_UID = "tnuid"
        private E_DOMAIN_UID = "duid"
        private E_DOMAIN_SID = "sid"
        private E_DOMAIN_SIDX = "vid"
        private E_USER_ID = "uid"
        private E_CONTEXT = "co"
        protected E_UNSTRUCTURED = "ue_pr"

        ' constants
        protected E_EVENT_UNSTRUCTURED = "ue"
        protected E_SCHEMA_UNSTRUCT_EVENT = "iglu:com.snowplowanalytics.snowplow/unstruct_event/jsonschema/1-0-0"
        private E_SCHEMA_PAYLOAD_DATA = "iglu:com.snowplowanalytics.snowplow/payload_data/jsonschema/1-0-4"
        private E_SCHEMA_CONTEXTS = "iglu:com.snowplowanalytics.snowplow/contexts/jsonschema/1-0-1"

        sub new(data as object)
            helpers = new Snowplow.Internal.Helpers()
            m.deviceTimestamp = helpers.currentTimestamp()
            deviceInfo = CreateObject("roDeviceInfo")
            m.eventId = deviceInfo.GetRandomUUID()

            if data[m.E_TRUE_TIMESTAMP] <> invalid then m.trueTimestamp = data[m.E_TRUE_TIMESTAMP]

            m.context = []
            if data.context <> invalid
                for each contextData in data.context
                    context = new Snowplow.Internal.EventContext(contextData)
                    m.addContext(context)
                end for
            end if
        end sub

        function preparePayloadToSend() as object
            payload = {}
            helpers = new Snowplow.Internal.Helpers()

            ' required properties
            payload[m.E_EVENT] = m.getEventName()
            payload[m.E_EID] = m.eventId
            payload[m.E_TRACKER_VERSION] = m.trackerVersion
            payload[m.E_PLATFORM] = m.platform
            payload[m.E_DEVICE_TIMESTAMP] = m.deviceTimestamp
            payload[m.E_SENT_TIMESTAMP] = helpers.currentTimestamp()
            payload[m.E_NAMESPACE] = m.trackerNamespace

            ' optional properties
            if m.domainUserId <> invalid then payload[m.E_DOMAIN_UID] = m.domainUserId
            if m.domainSessionId <> invalid then payload[m.E_DOMAIN_SID] = m.domainSessionId
            if m.domainSessionIndex <> invalid then payload[m.E_DOMAIN_SIDX] = m.domainSessionIndex
            if m.networkUserId <> invalid then payload[m.E_NETWORK_UID] = m.networkUserId
            if m.userId <> invalid then payload[m.E_USER_ID] = m.userId
            if m.appId <> invalid then payload[m.E_APPID] = m.appId
            if m.trueTimestamp <> invalid then payload[m.E_TRUE_TIMESTAMP] = m.trueTimestamp
            if m.timezone <> invalid then payload[m.E_TIMEZONE] = m.timezone
            if m.language <> invalid then payload[m.E_LANGUAGE] = m.language
            if m.resolution <> invalid then payload[m.E_RESOLUTION] = m.resolution
            if m.viewport <> invalid then payload[m.E_VIEWPORT] = m.viewport

            ' context
            if m.context.count() > 0
                contextData = []
                for each context in m.context
                    contextData.push(context.prepareDataToSend())
                end for
                payload[m.E_CONTEXT] = FormatJson({
                    schema: m.E_SCHEMA_CONTEXTS,
                    data: contextData
                })
            end if

            return payload
        end function

        function getEventName() as dynamic
            return invalid
        end function

        sub addContext(entity as object)
            m.context.push(entity)
        end sub

        function prepareDataToSend() as object
            data = {}
            data[m.E_DATA] = [m.preparePayloadToSend()]
            data[m.E_SCHEMA] = m.E_SCHEMA_PAYLOAD_DATA
            return data
        end function

        sub describe(logger)
            logger.debug(`+++ Snowplow Event ${m.getEventName()}: ${m.trackerNamespace} namespace, ${m.context.count()} context`)
            m.describeContext(logger)
        end sub

        protected sub describeContext(logger)
            for each context in m.context
                context.describe(logger)
            end for
        end sub
    end class
end namespace
